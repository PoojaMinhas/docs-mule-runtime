= everyEntry
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[everyentry1]]
== everyEntry&#40;Object, &#40;value: Any, key: Key&#41; &#45;&#62; Boolean&#41;: Boolean

Returns `true` if every entry in the object matches the condition.

The function stops iterating after the first negative evaluation of an
element in the array.

_Introduced in DataWeave 2.3.0. Supported by Mule 4.3 and later._

=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| object | The object to be evaluated
| condition | The condition that will be verified for every element
|===

=== Example

This example shows how the `everyEntry` behaves under different inputs.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
import everyEntry from dw::core::Objects
output application/json
---
{
    a: {} everyEntry (value, key) -> value is String,
    b: {a: "", b: "123"} everyEntry (value, key) -> value is String,
    c: {a: "", b: 123} everyEntry (value, key) -> value is String,
    d: {a: "", b: 123} everyEntry (value, key) -> key as String == "a",
    e: {a: ""} everyEntry (value, key) -> key as String == "a",
    f: null everyEntry ((value, key) -> key as String == "a")
}

----

==== Output

[source,Json,linenums]
----
{
  "a": true,
  "b": true,
  "c": false,
  "d": false,
  "e": true,
  "f": true
}
----


[[everyentry2]]
== everyEntry&#40;Null, &#40;Nothing, Nothing&#41; &#45;&#62; Boolean&#41;: Boolean

Helper function that makes *everyEntry* work with null values

_Introduced in DataWeave 2.3.0. Supported by Mule 4.3 and later._

